import org.jetbrains.dokka.gradle.DokkaTask

plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'org.jetbrains.kotlin.plugin.parcelize'
    id 'org.jetbrains.dokka'
    id 'maven-publish'
}

// JitPack uses the GitHub username/org as the group
// group = 'com.github.tdlibx'
group = 'com.github.akrafts-gpt'
version = '1.8.56-beta1'

android {
    namespace 'kotlinx.telegram'
    compileSdk 36

    defaultConfig {
        minSdk 21
        targetSdk 36
    }

    buildTypes {
        release {
            minifyEnabled false
            consumerProguardFiles 'proguard-rules.txt'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = '17'
    }

    lint {
        disable 'InvalidPackage'
    }

    // Modern Android Publishing configuration
    publishing {
        singleVariant("release") {
            withSourcesJar()
        }
    }
}

dependencies {
    // We use 'api' so the app gets access to TDLib classes too
    api 'com.github.tdlibx:td:1.8.56'
    implementation 'androidx.core:core-ktx:1.17.0'
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"
}

// Publishing block for JitPack
afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release
                groupId = project.group
                artifactId = 'td-ktx'
                version = '1.8.56-beta1'
            }
        }
    }
}

tasks.register('dokkaMarkdown', DokkaTask) {
    outputFormat = 'gfm'
    outputDirectory = "$rootDir/wiki"
    dokkaSourceSets {
        named("main") {
            includes.from('src/main/java/kotlinx/telegram/index.md')
            externalDocumentationLink {
                url.set(new URL("https://tdlibx.github.io/td/docs/"))
            }
        }
    }
}