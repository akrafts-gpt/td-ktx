plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'org.jetbrains.kotlin.kapt'
    id 'org.jetbrains.kotlin.plugin.serialization'
    id 'dagger.hilt.android.plugin'
}

def readTelegramCredential(String propertyKey, String envKey) {
    def propertyValue = project.findProperty(propertyKey)?.toString()?.trim()
    if (propertyValue) {
        return propertyValue
    }

    def envValue = System.getenv(envKey)?.toString()?.trim()
    if (envValue) {
        return envValue
    }

    throw new GradleException("Missing Telegram credential. Provide '$propertyKey' Gradle property or '$envKey' environment variable.")
}

android {
    namespace 'com.telegramflow.example'
    compileSdk 34

    defaultConfig {
        applicationId "com.telegramflow.example"
        minSdk 24
        targetSdk 34
        versionCode 20240621
        versionName "0.1.0"

        def telegramAppId = readTelegramCredential("telegramAppId", "TELEGRAM_APP_ID").toInteger()
        def telegramAppHash = readTelegramCredential("telegramAppHash", "TELEGRAM_APP_HASH")

        buildConfigField "int", "TELEGRAM_APP_ID", telegramAppId.toString()
        buildConfigField "String", "TELEGRAM_APP_HASH", "\"${telegramAppHash}\""

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
    }

    buildTypes {
        release {
            minifyEnabled = false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        benchmark {
            signingConfig signingConfigs.debug
            matchingFallbacks = ['release']
            debuggable = false
        }
        debug {
            debuggable = true
        }
        create("deepDebug") {
            initWith(getByName("debug"))
            applicationIdSuffix ".deepdebug"
            versionNameSuffix "-deep-debug"
            matchingFallbacks = ['debug']
            signingConfig signingConfigs.debug
            debuggable = true
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
    }
    buildFeatures {
        compose true
        buildConfig true
    }
    composeOptions {
        kotlinCompilerExtensionVersion compose_compiler_version
    }
    packaging {
        resources {
            excludes += "/META-INF/{AL2.0,LGPL2.1}"
        }
    }
}

dependencies {
    implementation platform("androidx.compose:compose-bom:$compose_bom_version")
    androidTestImplementation platform("androidx.compose:compose-bom:$compose_bom_version")

    implementation 'androidx.constraintlayout:constraintlayout-compose:1.0.1'

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    androidTestImplementation 'androidx.compose.ui:ui-test-junit4'

    implementation 'androidx.activity:activity-compose:1.9.0'

    implementation 'androidx.compose.ui:ui'
    implementation 'androidx.compose.material:material'
    implementation 'androidx.compose.material3:material3'
    implementation 'androidx.compose.runtime:runtime-livedata'
    implementation 'androidx.compose.foundation:foundation'
    implementation 'androidx.compose.foundation:foundation-layout'
    implementation 'androidx.compose.material:material-icons-extended'
    implementation 'androidx.paging:paging-compose:3.3.0'
    debugImplementation 'androidx.compose.ui:ui-tooling'
    debugImplementation 'androidx.compose.ui:ui-test-manifest'
    deepDebugImplementation 'androidx.compose.ui:ui-tooling'
    deepDebugImplementation 'androidx.compose.ui:ui-test-manifest'

    implementation "com.google.dagger:hilt-android:$hilt_version"
    kapt "com.google.dagger:hilt-compiler:$hilt_version"

    implementation 'androidx.hilt:hilt-navigation-compose:1.2.0'

    implementation 'androidx.navigation:navigation-compose:2.8.0'

    implementation 'org.jetbrains.kotlinx:kotlinx-serialization-json:1.6.3'

    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_version"

    implementation 'com.github.tdlibx:td:1.8.6'
    implementation project(':libtd-ktx')
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.8.2'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.8.2'
}
