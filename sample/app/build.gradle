plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'org.jetbrains.kotlin.kapt'
    id 'org.jetbrains.kotlin.plugin.serialization'
    id 'dagger.hilt.android.plugin'
    id 'org.jetbrains.kotlin.plugin.compose'  version "2.2.0"
}

def readTelegramCredential(String propertyKey, String envKey) {
    def propertyValue = project.findProperty(propertyKey)?.toString()?.trim()
    if (propertyValue) {
        return propertyValue
    }

    def envValue = System.getenv(envKey)?.toString()?.trim()
    if (envValue) {
        return envValue
    }

    throw new GradleException("Missing Telegram credential. Provide '$propertyKey' Gradle property or '$envKey' environment variable.")
}

android {
    namespace 'com.telegramflow.example'
    compileSdk 36

    defaultConfig {
        applicationId "com.telegramflow.example"
        minSdk 24
        targetSdk 36
        versionCode 20240621
        versionName "0.1.0"

        def telegramAppId = readTelegramCredential("telegramAppId", "TELEGRAM_APP_ID").toInteger()
        def telegramAppHash = readTelegramCredential("telegramAppHash", "TELEGRAM_APP_HASH")

        buildConfigField "int", "TELEGRAM_APP_ID", telegramAppId.toString()
        buildConfigField "String", "TELEGRAM_APP_HASH", "\"${telegramAppHash}\""

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }

    }

    buildTypes {
        release {
            minifyEnabled = true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig = signingConfigs.debug
        }
        debug {
            debuggable = true
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
    }
    buildFeatures {
        compose true
        buildConfig true
    }
    composeOptions {
        kotlinCompilerExtensionVersion compose_compiler_version
    }
    packaging {
        resources {
            excludes += "/META-INF/{AL2.0,LGPL2.1}"
        }
    }
}

dependencies {
    implementation platform("androidx.compose:compose-bom:$compose_bom_version")
    androidTestImplementation platform("androidx.compose:compose-bom:$compose_bom_version")

    implementation 'androidx.constraintlayout:constraintlayout-compose:1.1.1'

    testImplementation 'junit:junit:4.13.2'
    testImplementation 'io.mockk:mockk:1.13.12'
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$coroutines_version"
    androidTestImplementation 'androidx.test.ext:junit:1.3.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.7.0'
    androidTestImplementation 'androidx.compose.ui:ui-test-junit4'

    implementation 'androidx.activity:activity-compose:1.12.0'

    implementation 'androidx.compose.ui:ui'
    implementation 'androidx.compose.material:material'
    implementation 'androidx.compose.material3:material3'
    implementation 'androidx.compose.runtime:runtime-livedata'
    implementation 'androidx.compose.foundation:foundation'
    implementation 'androidx.compose.foundation:foundation-layout'
    implementation 'androidx.compose.material:material-icons-extended'
    implementation 'androidx.paging:paging-compose:3.3.6'
    implementation 'androidx.compose.ui:ui-tooling'
    debugImplementation 'androidx.compose.ui:ui-test-manifest'

    implementation 'io.coil-kt:coil-compose:2.7.0'

    implementation "com.google.dagger:hilt-android:$hilt_version"
    kapt "com.google.dagger:hilt-compiler:$hilt_version"

    implementation 'androidx.hilt:hilt-navigation-compose:1.3.0'

    implementation 'androidx.navigation:navigation-compose:2.9.6'

    implementation 'org.jetbrains.kotlinx:kotlinx-serialization-json:1.9.0'

    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_version"

    debugImplementation project(':libtd-ktx')

    releaseImplementation 'com.github.akrafts-gpt:td-ktx:1.8.56-beta5'

    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.10.0'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.10.0'
}